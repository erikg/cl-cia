;;;; cl-cia.lisp

(in-package #:cl-cia)

(defun read-file-to-list (file)
  (with-open-file (stream file)
    (loop for line = (read-line stream nil)
       while line collect line)))

(defun datify (datestr)
  "Attempt to convert a string to a local-time:timestamp object, using local-time:now if unable"
  ; and it always seems unable.
  (if datestr
      (handler-case
	  (local-time:parse-timestring datestr)
	(local-time::invalid-timestring () (local-time:now)))
      (local-time:now)))

(defparameter +funny-field-names+ '("Added Paths"
				    "Directory Properties"
				    "Log Message"
				    "Modified Paths"
				    "New Revision"
				    "Property changes on"
				    "Removed Paths"
				    "Revision Links"))

(defun isarg (line)
  (when line
    (multiple-value-bind (start end) (cl-ppcre:scan "^[A-Z][A-Za-z0-9-_]*:" line)
      (if start (values start end)
	  (let ((match (find (subseq line 0 (position #\: line)) +funny-field-names+ :test #'string=)))
	    (when match
	      (values 0 (+ (length match) 1))))))))

(defun trimmulti (lines offset)
  ""
  (if (<= (length (car lines)) offset)
      (cdr lines)
      (cons (string-trim " " (subseq (car lines) offset)) (cdr lines))))

(defun fieldinate (lines)
  "Given a set of lines following the email field format, build an association list of fields"
  (when lines
    (multiple-value-bind (start end) (isarg (car lines))
      (unless start (return-from fieldinate '()))
      (let ((nextargline (position-if #'isarg (cdr lines))))
	(let ((argname (subseq (car lines) start (- end 1))))
	  (cons
	   (cons argname
		 (if (or (not nextargline) (>= nextargline 1))
		     (trimmulti (subseq lines 0 (when nextargline (+ nextargline 1))) end)
		     (list (string-trim " " (subseq (car lines) end)))))
	   (fieldinate (if nextargline
			   (nthcdr (+ nextargline 1) lines)
			   (cdr lines)))))))))

(defun split-mail-to-head-and-body (file)
  (alexandria:when-let ((lines (read-file-to-list file)))
    (alexandria:when-let ((p (position-if (lambda (x) (string= "" x)) lines)))
      (cons (subseq lines 0 p) (subseq lines (+ p 1))))))

(defun flob (mailfile)
  (let ((l (split-mail-to-head-and-body mailfile)))
    (values (fieldinate (car l)) (fieldinate (cdr l)))))

(defun mail-element (name set)
  (let ((l (remove "" (mapcar (lambda (x) (string-trim " " x)) (cdr (assoc name set :test #'string=))) :test #'string=)))
    (if (cdr l) l (car l))))
(defun load-commit-from-mail-message (raw-header raw-body)
  (let ((header (fieldinate raw-header))
	(body (fieldinate raw-body)))
    (let ((list-id (mail-element "List-Id" header))
	  ;; favor the body Date node, but fall back to header if needed
	  (date (datify (or (mail-element "Date" body) (mail-element "Date" header))))
	  (revision (or (mail-element "Revision" body) (mail-element "New Revision" body)))
	  (author (mail-element "Author" body))
	  ;; the actual commit log message
	  (log (or (mail-element "Log" body) (mail-element "Log Message" body)))
	  (files (or (mail-element "Modified Paths" body) (alexandria:flatten
							   (list
							    (mail-element "Modified" body)
							    (mail-element "Added" body)
							    (mail-element "Deleted" body)))))
	  (project '())
	  (url (mail-element "URL" body)))
      (when (and list-id date revision author log)
	;; SVN::Notify likes to shove "-----" lines in, so try to eat those
	(when (and (listp log) (cl-ppcre:scan "^-*$" (car log)) (pop log)))
	(when (and (listp files) (cl-ppcre:scan "^-*$" (car files))) (pop files))
	(setf project (find-project-by-list-id
		       (let ((list-id (mail-element "List-Id" header)))
			 (if (listp list-id)
			     (format nil "狺扉篝殇扉篝殇┅┅麒孱扉篝蝈鲩箝镱躅戾篌躜麒孱矧篝蜷铉羼踽㈣趑鸷篚怏羼ㄣ徜蝈鲩箝镱珐篝蜷铉羼踽㈣趑痼函篚怏羼ㄣ徜蝈鲩箝镱俯┅箦翩躜ㄣ徜蝈鲩箝镱┅┅箦翩蝈鲩箝镱ㄣ狎蝈鲩箝镱┅麒孱ㄡ钿痱镪邈蝈鲩箝镱狨翳矧鲠祯弩磲脲轭篝犷沐с镯黹烘殪弩骈戾候弼轶轱蝈鲩箝镱轰狒溽翦乎箦狨翳矧乎蜢躜喉弩筢珏祜绌痱镪邈舂┅┅ㄤ彐躅痱镢弩蟓磲殪溟颦徕篝蜥泗ㄦ躅磲殪溟痱镢弩箦洵磲殪溟栾镫螬ㄢ矧溴狨翳蝈徜蠛鏖翳祜汶桢熹í忾珈镢氇ㄤ镬轶ㄦ殪ㄣ飙驷浜扉篝溟蝈泗矧磲殪溟颟眭祠轲戾鲠祯瀛忾钿礤篌徵痱镪邈舂ㄡ痧禊骢钽戾è箴扉舡磲殪麸桢徜犷洵怙澌骈戾┅扉篝ㄣ狎飑ㄣ潋飑┅麒孱ㄡ滗礤篌徵痱镪邈礤篌徵濠戾è蝈ㄩ栾镫磲疸狎灬礅溽ㄦ躅汜祆礤篌徵濠栾镫螬Ж舂┅躅戾篌ㄦ轭铋蝈螬蝈钺礤骈戾骈戾礤蜱瀛疳翳钺礤痱镢弩箦洵磲殪溟ㄦ殪瀛钺礤篝蜷铉骈戾┅┅┅┅┅ㄤ彐躅痱镢弩蟓磲殪溟é脲磲殪溟溻躅痱镢弩箦洵磲殪溟颢痱镢弩箦洵磲殪溟溻痱镢弩箦洵磲殪溟颢ㄨ镲塍Ж┅⑿狎箦犰礤篌徵弩轭磲殪溟颥徜溟铉疳蝮邃泔眄轸礤篌徵弩麸翳扉篝犷狃痨轭翳栾镫螈痱镢弩蟓磲殪溟颦徕篝蜥泗＇祜徜泔眄轸骝镯磲殪礤篌徵磲殪溟痱镢弩箦洵磲殪溟栾镫螬ㄤ彐躅痱镢弩蟓盱磲殪溟é脲磲殪溟溻躅痱镢弩箦洵盱磲殪溟颢痱镢弩箦洵磲殪溟溻痱镢弩箦洵盱磲殪溟颢ㄨ镲塍Ж┅⑿狎箦犰礤篌徵弩轭磲殪溟颥徜溟铉疳蝮邃泔眄轸礤篌徵弩麸翳扉篝犷狃痨轭翳栾镫螈痱镢弩蟓磲殪溟颦徕篝蜥泗灬礅溽ㄨ遽溴怙澌栾镫螬ㄤ邈灬蝈ㄩ珙矧桢徜弪栾镫螬戾è礤篌徵弩Ж┅痱镪邈Ж┅ㄤ镬轶盱疳蝮屮盱ㄦ矧磲铋狺怙澌┅戾è礤篌徵ㄣ徜潋盱┅躅戾篌痱镪邈箦翩痱镪邈ㄦ轭洵痱镪邈ㄣ狎盱┅┅麒孱ㄡ钿篝蜷铉羼踽ㄣ狎盱⒙姨昧蘑篝蜷铉羼踽ㄣ徜盱㈣趑鸷怛煦徜矧纰┅箦翩痱镪邈ㄦ轭洵痱镪邈⑩蜢汜鏖腴┅瘐箬礤篌徵礤篌徵弩┅鲠祯弩礤篌徵弩痱镪邈舂┅磲殪溟痱镢弩箦洵磲殪溟栾镫螬ㄤ彐躅瘐眇ī痱镢弩蟓磲殪溟颟痱镢弩蟓盱磲殪溟颟筢鲥篝狒濠ㄤ彐鲠瘐眇Ж┅ㄤ彐鲠瘐眇蝓铑轭绐Ж┅ㄤ彐躅篝狎舡瘐眇ī躅戾篌ㄡ钿瘐眇ㄢ矧溴狨翳蝈徜蠛翳蝈徜瘐眇ㄢ矧溴狨翳蝈徜蠛翳蝈徜犰轹瀛瘐眇┅箦翩瘐眇蝓铑轭绐舂箦翩瘐眇ㄢ矧溴狨翳蝈徜蠛磲脲翳蝈徜灬礅溽ī祜镳麒殪瘐眇蝓铑轭绐滹痱镧瘐眇箪邋旦┅侯犴沆汩瘐眇弪┅┅ㄤ彐躅篝镳瘐眇ī麒孱ㄡ钿瘐眇瘐眇蝓铑轭绐箦翩瘐眇蝓铑轭绐Ж┅ㄢ矧溴狨翳蝈徜蠛觑轭翳蝈徜瘐眇箦翩瘐眇Ж┅┅